---
layout: tutorial
title: Hello HDP An Introduction to Hadoop with Hive and Pig
tutorial-id: 100
tutorial-series: Basic Development
tutorial-version: hdp-2.4.0
intro-page: true
components: [ ambari, hive, pig, spark, zeppelin ]
---

# Lab 3: Pig - Risk Factor

## Use Pig to compute Driver Risk Factor

### Introduction

In this tutorial you will be introduced to Apache Pig. In the earlier section of lab you learned how to load data into HDFS and then manipulate it using Hive. We are using the Truck sensor data to better understand  risk associated with every driver. This section will teach you to compute risk using Apache Pig.

## Pre-Requisites

The tutorial is a part of series of hands on tutorial to get you started on HDP using Hortonworks sandbox. Please ensure you complete the prerequisites before proceeding with this tutorial.

*   Downloaded and Installed latest [Hortonworks Sandbox](http://hortonworks.com/products/hortonworks-sandbox/#install)
*   [Learning the Ropes of the Hortonworks Sandbox](http://hortonworks.com/hadoop-tutorial/learning-the-ropes-of-the-hortonworks-sandbox/)
*   Lab 1: Loading sensor data into HDFS
*   Lab 2: Data Manipulation with Apache Hive
*   Allow yourself around one hour to complete this tutorial.

## Outline

*   [Pig Basics](#pig-basics)
*   [Step 3.1: Define Table Schema](#step3.1)
*   [Step 3.2: Create Pig Script](#step3.2)
*   [Step 3.3: Quick Recap](#step3.3)
*   [Step 3.4: Execute Pig Script on Tez](#step3.4)
*   [Suggested Readings](#suggested-readings)

## Pig Basics <a id="pig-basics"></a>

Pig is a high-level scripting language that is used with Apache Hadoop. Pig enables data workers to write complex data transformations without knowing Java. Pig’s simple SQL-like scripting language is called Pig Latin, and appeals to developers already familiar with scripting languages and SQL.

Pig is complete, so you can do all required data manipulations in Apache Hadoop with Pig. Through the User Defined Functions(UDF) facility in Pig, Pig can invoke code in many languages like JRuby, Jython and Java. You can also embed Pig scripts in other languages. The result is that you can use Pig as a component to build larger and more complex applications that tackle real business problems.

Pig works with data from many sources, including structured and unstructured data, and store the results into the Hadoop Data File System.

Pig scripts are translated into a series of MapReduce jobs that are run on the Apache Hadoop cluster.

### Step 3.1: Define Table Schema <a id="step3.1"></a>

Now we have refined the truck data to get the average mpg  for each truck. The next task is to compute the risk factor for each driver which is the total miles driven/abnormal events. We can get the event information from the geolocation table.


![Lab3_1](/assets/hello-hdp/Lab3_1.png)


If we look at the truck_mileage table, we we have the driverid and the number of miles for each trip. To get the total miles for each driver, we can group those records by driverid and then sum the miles.

#### 3.1.1 Create Table DriverMileage from Existing truck_mileage Data

We will start by creating a table named driver_mileage that is created from a query of the columns we want from truck_mileage. The following query groups the records by driverid and sums the miles in the select statement. Execute the query below in a new Worksheet:

~~~
CREATE TABLE DriverMileage
STORED AS ORC
AS
SELECT driverid, sum(miles) totmiles
FROM truck_mileage
GROUP BY driverid;
~~~

![create_table_driver_mileage](/assets/hello-hdp/driver_mileage_table_hello_hdp_lab3.png)

#### 3.1.2 View Data Generated by Query

To view data, click the **Load sample data** icon in the Database Explorer next to drivermileage. The results should look like:


![Lab3_2](/assets/hello-hdp/Lab3_2.png)


#### 3.1.3 Create Table riskfactor from Existing trucks_mileage Data

Next, you will use Pig to compute the risk factor of each driver. Before we can run the Pig code, one of the requirements for the HCatStorer() class is that the table must already exist in Hive. The Pig code expects the following structure for a table named **riskfactor**. Execute the following DDL command:

~~~
CREATE TABLE riskfactor (driverid string,events bigint,totmiles bigint,riskfactor float)
STORED AS ORC;
~~~

![riskfactor_table](/assets/hello-hdp/riskfactor_table_hello_hdp_lab3.png)

#### 3.1.4 Verify Table riskfactor was Created Successfully

Verify the riskfactor table was created successfully. It will be empty now, but you will populate it from a Pig script. You are now ready to compute the risk factor using Pig. Let’s take a look at Pig and how to execute Pig scripts from within Ambari.

### Step 3.2: Create Pig Script <a id="step3.2"></a>

In this tutorial we create and run a Pig script. We will use the Ambari Pig User View. Let’s get started…

#### 3.2.1 Log in to Ambari Pig User Views

To get to the Ambari Pig User View, click on the User Views icon at top right and select **Pig**:


![Screen Shot 2015-07-21 at 10.12.41 AM](/assets/hello-hdp/pig_user_view_icon_hello_hdp_lab3.png)  


This will bring up the Ambari Pig User View interface. Your Pig View does not have any scripts to display, so it will look like the following:

![Lab3_4](/assets/hello-hdp/Lab3_4.png)


On the left is a list of your scripts, and on the right is a composition box for writing scripts. A special feature of the interface is the Pig helper located below the name of your script file. The Pig helper will provide us with templates for the statements, functions, I/O statements, HCatLoader() and Python user defined functions. At the very bottom are status areas that will show the results of our script and log files.

The following screenshot shows and describes the various components and features of the Pig User View:


![Lab3_5](/assets/hello-hdp/pig_user_view_components_hello_hdp.png)


#### 3.2.2 Create a New Script

Let’s enter a Pig script. Click the **New Script** button in the upper-right corner of the view:


![Lab3_6](/assets/hello-hdp/new_script_hello_hdp_lab3.png)


Name the script **riskfactor.pig**, then click the **Create** button:


![Lab3_7](/assets/hello-hdp/Lab3_7.png)


#### 3.2.3 Load Data in Pig using Hcatalog

We are going to use HCatalog to load data into Pig. HCatalog allows us to share schema across tools and users within our Hadoop environment. It also allows us to factor out schema and location information from our queries and scripts and centralize them in a common repository. Since it is in HCatalog we can use the HCatLoader() function. Pig makes it easy by allowing us to give the table a name or alias and not have to worry about allocating space and defining the structure. We just have to worry about how we are processing the table.

* We can use the Pig helper located below the name of your script file to give us a template for the line. Click on **Pig helper -> HCatalog->load template**
* The entry **%TABLE%** is highlighted in red for us. Type the name of the table which is geolocation.
* Remember to add the **a =** before the template. This saves the results into a. Note the **‘=’** has to have a space before and after it.
* Our completed line of code will look like:

~~~
a = LOAD 'geolocation' using org.apache.hive.hcatalog.pig.HCatLoader();
~~~

Copy-and-paste the above Pig code into the riskfactor.pig window.

#### 3.2.4 Filter your data set

The next step is to select a subset of the records so that we just have the records of drivers for which the event is not normal. To do this in Pig we use the Filter operator. We tell Pig to Filter our table and keep all records where event !=“normal” and store this in b. With this one simple statement Pig will look at each record in the table and filter out all the ones that do not meet our criteria.

* We can use Pig Help again by clicking on **Pig helper->Relational Operators->FILTER template**
* We can replace **%VAR%** with **“a”** (hint: tab jumps you to the next field)
* Our **%COND%** is “**event !=’normal’;** ” (note: single quotes are needed around normal and don’t forget the trailing semi-colon)
* Complete line of code will look like:

~~~
b = filter a by event != 'normal';
~~~

Copy-and-paste the above Pig code into the riskfactor.pig window.

#### 3.2.5 Iterate your data set

Now that we have the right set of records we can iterate through them. We use the “foreach” operator on the grouped data to iterate through all the records. We would also like to know how many times a driver has a non normal event associated with him. to achieve this we add ‘1’ to every row in the data set.

* Pig helper ->Relational Operators->FOREACH template will get us the code
* Our **%DATA%** is **b** and the second **%NEW_DATA%** is “**driverid,event,(int) ‘1’ as occurance;**”
* Complete line of code will look like:

~~~
c = foreach b generate driverid, event, (int) '1' as occurance;
~~~

Copy-and-paste the above Pig code into the riskfactor.pig window:

#### 3.2.6 Calculate the total non normal events for each driver

The group statement is important because it groups the records by one or more relations. In this case we would like to group by driver id and iterate over each row again to sum the non normal events.

* **Pig helper ->Relational Operators->GROUP %VAR% BY %VAR%** template will get us the code
* First **%VAR%** takes **“c”** and second **%VAR%** takes “**driverid;**”
* Complete line of code will look like:

~~~
d = group c by driverid;
~~~

Copy-and-paste the above Pig code into the riskfactor.pig window.

* Next use Foreach statement again to add the occurance.

~~~
e = foreach d generate group as driverid, SUM(c.occurance) as t_occ;
~~~


#### 3.2.7 Load drivermileage Table and Perform a Join Operation

In this section we will load drivermileage table into Pig using Hcatlog and perform a join operation on driverid. The resulting data set will give us total miles and total non normal events for a particular driver.

* Load drivermileage using HcatLoader()

~~~
g = LOAD 'drivermileage' using org.apache.hive.hcatalog.pig.HCatLoader();
~~~

* **Pig helper ->Relational Operators->JOIN %VAR% BY** template will get us the code
* Replace **%VAR%** by ‘**e**’ and after **BY** put ‘**driverid, g by driverid;**’
* Complete line of code will look like:

~~~
h = join e by driverid, g by driverid;
~~~

Copy-and-paste the above two Pig codes into the riskfactor.pig window.

#### 3.2.8 Compute Driver Risk factor

In this section we will associate a driver risk factor with every driver. Driver risk factor will be calculated by dividing total miles travelled by non normal event occurrences.

* We will use Foreach statement again to compute driver risk factor for each driver.
* Use the following code and paste it into your Pig script.

~~~
final_data = foreach h generate $0 as driverid, $1 as events, $3 as totmiles, (float) $3/$1 as riskfactor;
~~~

* As a final step store the data into a table using Hcatalog.

~~~
store final_data into 'riskfactor' using org.apache.hive.hcatalog.pig.HCatStorer();
~~~
Here is the final code and what it will look like once you paste it into the editor.


**Geolocation has data stored in ORC format**

~~~
a = LOAD 'geolocation' using org.apache.hive.hcatalog.pig.HCatLoader();
b = filter a by event != 'normal';
c = foreach b generate driverid, event, (int) '1' as occurance;
d = group c by driverid;
e = foreach d generate group as driverid, SUM(c.occurance) as t_occ;
g = LOAD 'drivermileage' using org.apache.hive.hcatalog.pig.HCatLoader();
h = join e by driverid, g by driverid;
final_data = foreach h generate $0 as driverid, $1 as events, $3 as totmiles, (float) $3/$1 as riskfactor;
store final_data into 'riskfactor' using org.apache.hive.hcatalog.pig.HCatStorer();
~~~


![Lab3_8](/assets/hello-hdp/pig_script_compute_driver_riskfactor_hello_hdp_lab3.png)


Save the file riskfactor.pig by clicking the **Save** button in the left-hand column.

### Step 3.3: Quick Recap <a id="step3.3"></a>

Before we execute the code, let’s review the code again:

* The line `a=` loads the geolocation table from HCatalog.
* The line `b=` filters out all the rows where the event is not ‘Normal’.
* Then we add a column called occurrence and assign it a value of 1.
* We then group the records by driverid and sum up the occurrences for each driver.
* At this point we need the miles driven by each driver, so we load the table we created using Hive.
* To get our final result, we join by the driverid the count of events in e with the mileage data in g.
* Now it is real simple to calculate the risk factor by dividing the miles driven by the number of events

You need to configure the Pig Editor to use HCatalog so that the Pig script can load the proper libraries. In the Pig arguments text box, enter –**useHCatalog**   and click the **Add** button:

**Note** that this argument is **case sensitive**. It should be typed exactly "-useHCatalog".


![Lab3_9](/assets/hello-hdp/Lab3_9.png)


The **Arguments** section of the Pig View should now look like the following:  
![Lab3_10](/assets/hello-hdp/Lab3_10.png)


### Step 3.4: Execute Pig Script on Tez <a id="step3.4"></a>

#### 3.4.1 Execute Pig Script

Click Execute on Tez checkbox and finally hit the blue **Execute** button to submit the job. Pig job will be submitted to the cluster. This will generate a new tab with a status of the running of the Pig job and at the top you will find a progress bar that shows the job status.


![Lab3_11](/assets/hello-hdp/execute_pig_script_compute_riskfactor_hello_hdp_lab3.png)


#### 3.4.2 View Results Section  

Wait for the job to complete. The output of the job is displayed in the **Results** section. Notice your script does not output any result – it stores the result into a Hive table – so your Results section will be empty.


![Lab3_12](/assets/hello-hdp/running_script_riskfactor_hello_hdp_lab3.png)

![Lab3_13](/assets/hello-hdp/completed_riskfactor_script_hello_hdp_lab3.png)


Click on the **Logs** dropdown menu to see what happened when your script ran. Errors will appear here.

#### 3.4.3 Verify Pig Script Successfully Populated Hive Table 

Go back to the Ambari Hive User View and browse the data in the **riskfactor** table to verify that your Pig job successfully populated this table. Here is what is should look like:


![Lab3_14](/assets/hello-hdp/pig_populated_riskfactor_table_hello_hdp_lab3.png)


At this point we now have our truck miles per gallon table and our risk factor table. The next step is to pull this data into Excel to create the charts for the visualization step.

## Suggested Readings <a id="suggested-readings"></a>

Strengthen your foundation of pig latin and reinforce why this scripting platform is benficial for processing and analyzing massive data sets with these resources:

* [Apache Pig](http://hortonworks.com/hadoop/pig/)
* [Programming Pig](http://www.amazon.com/Programming-Pig-Alan-Gates/dp/1449302645/ref=sr_1_2?ie=UTF8&qid=1455994738&sr=8-2&keywords=pig+latin&refinements=p_72%3A2661618011)
* [Pig Latin Basics](http://pig.apache.org/docs/r0.14.0/basic.html)

